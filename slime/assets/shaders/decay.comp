#version 460
layout(local_size_x = 1, local_size_y = 1) in;

struct Parameters {
	float movementSpeed;
	float decayRate;
	float diffuseRate;
	float rotationAngle;
	float sensorAngle;
	float sensorDistance;
	int   sensorSize;
	float _padding;
};

layout(rgba32f, binding = 0) uniform image2D trailMap;
layout(rgba32f, binding = 1) uniform image2D diffusedTrailMap;
layout(std140, binding = 2) uniform ParametersBlock {
	Parameters parameters;
};

layout(location = 0) uniform float deltaTime;
layout(location = 1) uniform ivec2 size;

void main() {
	ivec2 id = ivec2(gl_GlobalInvocationID.xy);

	vec3 sum = vec3(0.0);
	for (int offsetY = -1; offsetY <= 1; ++offsetY) {
		for (int offsetX = -1; offsetX <= 1; ++offsetX) {
			ivec2 offset = ivec2(offsetX, offsetY);
			sum +=
			    imageLoad(trailMap, ivec2(mod(id + offset + size, size))).rgb;
		}
	}
	sum /= 9.0;

	vec3 originalValue = imageLoad(trailMap, id).xyz;

	vec3 diffusedValue =
	    mix(originalValue,
	        sum,
	        min(1.0, max(0.0, parameters.diffuseRate * deltaTime)));
	diffusedValue =
	    max(vec3(0.0), diffusedValue - parameters.decayRate * deltaTime);

	imageStore(diffusedTrailMap, id, vec4(diffusedValue, 1.0));
}